
/*************************************************************************/
  
Package latexToGLSL;

/*************************************************************************/
    

Helpers


/* Caracteres */


 letra = ['a'..'z'] | ['A'..'Z'];
 digito = ['0'..'9'];
 espaco = ' ';
 tudo = [0..255];
 
 
/* Retorno */


 rc = 13; //carriage return
 nl = 10; //new line
 tab = 9;  //tabulacao
 tudo_sem_retorno = [tudo - [rc + nl]];
 tudo_sem_chave = [tudo - '}'];
 tudo_sem_aspas = [tudo - '''];
 fl_espaco = '\\';

/*************************************************************************/


Tokens


/* Palavras reservadas */


 tk_equation = 'equation';
 tk_array = 'array';
 tk_begin = '\begin';
 tk_end = '\end';
 tk_fl_lbrace = '\lbrace';
 tk_fl_rbrace = '\rbrace';
 tk_fl_operatorname = '\operatorname';
 tk_fl_displaystyle = '\displaystyle'; 
 tk_fl_mathbf = '\mathbf';
 tk_fl_sifao = '$';
 

/* Operadores */


 tk_igual = '=';
 tk_soma = '+';
 tk_subt = '-';
 tk_mult = '*';
 tk_div = '/';
 tk_mod = '%';
 tk_pot = '^';
 tk_par_left = '(';
 tk_par_right  = ')';
 tk_col_left  = '[';
 tk_col_right = ']';
 tk_chave_left  = '{';
 tk_chave_right = '}';
 tk_underline = '_';
 tk_virgula = ',';
 tk_pipe = '|';
 tk_maior = '>';
 tk_menor = '<';
 tk_maiorigual = '>=';
 tk_menorigual = '<=';
 
 
/* Fun��es Algebra Linear */ 
 
 
 tk_times = '\times'; /*cross product*/ 
 tk_cdot = '\cdot';
 tk_vec = '\vec';
 tk_sum = '\sum';
 tk_prod = '\prod';
 
 
/* Fun��es Matem�ticas */ 
 
 
 tk_min = '\min';
 tk_max = '\max';
 tk_frac = '\frac';
 tk_exp = '\exp';
 tk_log = '\log';
 tk_ln = '\ln';
 tk_lg = '\lg';
 tk_sqrt = '\sqrt';
 tk_lim = '\lim';
 tk_to = '\to'; 
 tk_infty = '\infty';
 tk_integral = '\int';
 tk_degree = '\circ';
 tk_unf_letra = '\mathrm';
 tk_pipe_mid = '\mid';
 tk_leftarrow = '\leftarrow'; 
 tk_la_gets = '\gets';
 tk_rightarrow = '\rightarrow';
 tk_double_pipe = '\|';
 
 
/* Fun��es Trigonom�tricas */ 
	
 tk_cos = '\cos';
 tk_sin = '\sin';
 tk_tan = '\tan';
 tk_cot = '\cot';
 tk_arcsin = '\arcsin';
 tk_arccos = '\arccos';
 tk_arctan = '\arctan';
 tk_arccot = '\arccot';
 tk_sinh = '\sinh';
 tk_cosh = '\cosh';
 tk_tanh = '\tanh';
 tk_coth = '\coth';
 tk_sec = '\sec';
 tk_csc = '\csc';
 
 
/* Alfabeto Grego */
 
 
 tk_alpha = '\alpha';
 tk_beta = '\beta';
 tk_gamma = '\gamma';
 tk_up_gamma = '\Gamma';
 tk_delta = '\delta';
 tk_up_delta = '\Delta';
 tk_epsilon = '\epsilon';
 tk_varepsilon = '\varepsilon';
 tk_zeta = '\zeta';
 tk_eta = '\eta';
 tk_theta = '\theta';
 tk_up_theta = '\Theta';
 tk_vartheta = '\vartheta';
 tk_iota = '\iota';
 tk_kappa = '\kappa'; 
 tk_varkappa = '\varkappa';
 tk_lambda = '\lambda';
 tk_up_lambda = '\Lambda';
 tk_mu = '\mu';
 tk_nu = '\nu';
 tk_xi = '\xi';
 tk_up_xi = '\Xi';
 tk_pi = '\pi';
 tk_up_pi = '\Pi';
 tk_varpi = '\varpi';
 tk_rho = '\rho';
 tk_varrho = '\varrho';
 tk_sigma = '\sigma';
 tk_up_sigma = '\Sigma';
 tk_varsigma = '\varsigma';
 tk_tau = '\tau';
 tk_upsilon = '\upsilon';
 tk_up_upsilon = '\Upsilon';
 tk_phi = '\phi';
 tk_up_phi = '\Phi';
 tk_varphi = '\varphi';
 tk_chi = '\chi';
 tk_psi = '\psi';
 tk_up_psi = '\Psi';
 tk_omega = '\omega';
 tk_up_omega = '\Omega';
 
 
/* Caracteres */


 tk_num_inteiro = (digito+);
 tk_num_real = (digito+ ',' digito+);
 tk_identificador = (letra+); 


/* Ignorados */


 vazio = (espaco | rc | nl | tab)+;
 espaco = fl_espaco+;
/* comentario_linha = ('--' tudo_sem_retorno*); */
/* comentario_bloco = ('{' tudo_sem_chave* '}')+; */
 
 
/*************************************************************************/

Ignored Tokens

 vazio, espaco;

/*************************************************************************/

Productions 
         
 declaracao_inicial
         {-> a_bloco_equacoes  } =

 	 {declaracao_inicial} bloco_inicio bloco_equacoes bloco_fim  
         {-> bloco_equacoes.a_bloco_equacoes  }  
     ;

 bloco_equacoes 
 		 {-> a_bloco_equacoes  } =
         
 	 {equacao} equacao  
         {-> New a_bloco_equacoes.a_equacao ( equacao.a_equacao ) }   |
         
     {lista_equacoes} bloco_inicio_array lista_equacoes bloco_fim_array 
         {-> New a_bloco_equacoes.a_lista_equacoes ( [ lista_equacoes.a_equacao ] ) }  
     ;    
     
 lista_equacoes
 	 	 {-> a_equacao*  } = 
 	 	 
 	 {equacao} equacao  
         {-> [ equacao.a_equacao ] }   |
         
     {lista_equacoes} equacao tk_virgula lista_equacoes 
   		 {-> [ equacao.a_equacao, lista_equacoes.a_equacao  ] }
     ;
     
 equacao
         {-> a_equacao } =
     {equacao} tk_fl_displaystyle? tk_identificador tk_igual exp
         {-> New a_equacao ( tk_identificador, exp.a_exp ) }
     ;

 parametros
         {-> a_exp* } =
         
     {single} exp
         {-> [ exp.a_exp ]  }  |

     {multiple} exp tk_virgula parametros 
         {-> [ exp.a_exp, parametros.a_exp ]  }  
     ;

 exp
         {-> a_exp  } =

     {fator} fator 
         {-> fator.a_exp }   |

     {soma} exp tk_soma fator 
         {-> New a_exp.a_soma ( exp.a_exp , fator.a_exp  )  }   |

     {dim} exp tk_subt fator 
         {-> New a_exp.a_dim ( exp.a_exp , fator.a_exp  )  }  
     ;

 fator
         {-> a_exp  } =

     {termo} fator_expo 
         {-> fator_expo.a_exp  }   |

     {mult} fator tk_mult fator_expo 
         {-> New a_exp.a_mult ( fator.a_exp, fator_expo.a_exp )  }   |

     {div} fator tk_div fator_expo 
         {-> New a_exp.a_div ( fator.a_exp, fator_expo.a_exp )  }   |

     {mod} fator tk_mod fator_expo 
         {-> New a_exp.a_mod ( fator.a_exp, fator_expo.a_exp )  }  
     ;

 fator_expo
         {-> a_exp  } =

     {termo} termo 
         {-> termo.a_exp  }   |

     {pot} termo tk_pot fator_expo 
         {-> New a_exp.a_pot ( termo.a_exp, fator_expo.a_exp )  }  
     ;

 termo
         {-> a_exp  } =

     {valor} valor 
         {-> valor.a_exp  }   |

     {numero} numero 
         {-> numero.a_exp  }   |

     {fl_parenteses} fl_parenteses 
         {-> fl_parenteses.a_exp  }   |

     {fl_colchetes} fl_colchetes 
         {-> fl_colchetes.a_exp  }   |

     {fl_chaves} fl_chaves 
         {-> fl_chaves.a_exp  }   |

     {negativo} tk_subt termo 
        { -> New a_exp.a_negativo ( termo.a_exp ) }  
     ;

 numero
         {-> a_exp  } =

     {tk_num_inteiro} tk_num_inteiro 
         {-> New a_exp.a_tk_num_inteiro ( tk_num_inteiro  )  }   |

     {tk_num_real} tk_num_real 
         {-> New a_exp.a_tk_num_real ( tk_num_real  )  }  
     ;

 valor
         {-> a_exp  } =

	 {fl_reservado} fl_reservado 
         {-> fl_reservado.a_exp }   |
	 
     {identificador} tk_identificador 
         {-> New a_exp.a_identificador ( tk_identificador  )  }   |

     {id_modificado} valor tk_underline fl_chaves 
         {-> New a_exp.a_id_modificado ( valor.a_exp , fl_chaves.a_exp  )  }  
     ;

 fl_reservado
         {-> a_exp  } =

     {fl_algebra} fl_algebra 
         {-> fl_algebra.a_exp }   |

     {fl_matematica} fl_matematica 
         {-> fl_matematica.a_exp }   |

     {equacao_trig} equacao_trig 
         {-> equacao_trig.a_exp }   |

     {fl_alfabetogrego} fl_alfabetogrego 
         {-> fl_alfabetogrego.a_exp }   |

     {fl_operador_custom} tk_fl_operatorname tk_chave_left tk_identificador tk_chave_right tk_par_left parametros tk_par_right 
         {-> New a_exp.a_fl_operador_custom (tk_identificador,  [parametros.a_exp]  )  }   |

     {fl_style} tk_fl_displaystyle tk_identificador 
         {-> New a_exp.a_fl_style (tk_fl_displaystyle , tk_identificador  )  }  
     ;

 fl_algebra
         {-> a_exp  } =

     {times} fa_times 
         {-> fa_times.a_exp }   |

     {cdot} fa_cdot 
         {-> fa_cdot.a_exp }   |

     {sum} fa_sum 
         {-> fa_sum.a_exp }   |

     {prod} fa_prod 
         {-> fa_prod.a_exp }   |

     {vec} fa_vec 
         {-> fa_vec.a_exp }   
     ;

 fa_times
         {-> a_exp  } =

     [left]: fl_chaves tk_times [right]: fl_chaves 
         {-> New a_exp.a_fa_times ( left.a_exp , right.a_exp )  }  
     ;

 fa_cdot
         {-> a_exp  } =

     [left]: fl_chaves tk_cdot [right]: fl_chaves 
         {-> New a_exp.a_fa_cdot ( left.a_exp , right.a_exp )  }  
     ;

 fa_sum
         {-> a_exp  } =

     tk_sum tk_underline [left]: fl_chaves tk_pot [right]: fl_chaves 
         {-> New a_exp.a_fa_sum ( left.a_exp , right.a_exp ) }  
     ;

 fa_prod
         {-> a_exp  } =

     tk_prod tk_underline [left]: fl_chaves tk_pot [right]: fl_chaves 
         {-> New a_exp.a_fa_prod ( left.a_exp , right.a_exp )  }  
     ;

 fa_vec
         {-> a_exp  } =

     {vetora} tk_vec tk_identificador 
         {-> New a_exp.a_vetora ( tk_identificador  )  }   |

     {vetorb} tk_vec fl_chaves 
         {-> New a_exp.a_vetorb ( fl_chaves.a_exp )  }  
     ;

 fl_matematica
         {-> a_exp  } =

     {fm_min} fm_min 
         {-> fm_min.a_exp }   |

     {fm_max} fm_max 
         {-> fm_max.a_exp }   |

     {fm_frac} fm_frac 
         {-> fm_frac.a_exp }   |

     {fm_exponencial} fm_exponencial 
         {-> fm_exponencial.a_exp }   |

     {fm_sqrt} fm_sqrt 
         {-> fm_sqrt.a_exp }   |

     {fm_lim} fm_lim 
         {-> fm_lim.a_exp }   |

     {fm_integral} fm_integral 
         {-> fm_integral.a_exp } 
     ;

 fm_min
         {-> a_exp  } =

     tk_min tk_par_left parametros tk_par_right 
         {-> New a_exp.a_fm_min ( [parametros.a_exp] )  }  
     ;

 fm_max
         {-> a_exp  } =

     tk_max tk_par_left parametros tk_par_right 
         {-> New a_exp.a_fm_max ( [parametros.a_exp] )  }  
     ;

 fm_frac
         {-> a_exp  } =

     tk_frac [left]: fl_chaves [right]: fl_chaves 
         {-> New a_exp.a_fm_frac ( left.a_exp , right.a_exp )  }  
     ;

 fm_sqrt
         {-> a_exp  } =

     tk_sqrt fl_chaves 
         {-> New a_exp.a_fm_sqrt ( fl_chaves.a_exp )  }  
     ;

 fm_lim
         {-> a_exp  } =

     tk_lim tk_underline tk_chave_left tk_identificador tk_to tk_infty tk_chave_right 
         {-> New a_exp.a_fm_lim ( tk_lim , tk_underline , tk_chave_left , tk_identificador , tk_to , tk_infty , tk_chave_right  )  }  
     ;

 fm_integral
         {-> a_exp  } =

     tk_integral tk_underline [left]: fl_chaves tk_pot [right]: fl_chaves 
         {-> New a_exp.a_fm_integral ( left.a_exp , right.a_exp  )  }  
     ;

 fm_exponencial
         {-> a_exp  } =

     tk_exp fl_colchetes 
         {-> New a_exp.a_fm_exponencial ( fl_colchetes.a_exp )  }  
     ;

 fl_trigonometrica
         {-> a_fl_trigonometrica  } =

     {tk_cos} tk_cos 
         {-> New a_fl_trigonometrica.tk_cos ( tk_cos  )  }   |

     {tk_sin} tk_sin 
         {-> New a_fl_trigonometrica.tk_sin ( tk_sin  )  }   |

     {tk_tan} tk_tan 
         {-> New a_fl_trigonometrica.tk_tan ( tk_tan  )  }   |

     {tk_cot} tk_cot 
         {-> New a_fl_trigonometrica.tk_cot ( tk_cot  )  }   |

     {tk_arcsin} tk_arcsin 
         {-> New a_fl_trigonometrica.tk_arcsin ( tk_arcsin  )  }   |

     {tk_arccos} tk_arccos 
         {-> New a_fl_trigonometrica.tk_arccos ( tk_arccos  )  }   |

     {tk_arctan} tk_arctan 
         {-> New a_fl_trigonometrica.tk_arctan ( tk_arctan  )  }   |

     {tk_arccot} tk_arccot 
         {-> New a_fl_trigonometrica.tk_arccot ( tk_arccot  )  }   |

     {tk_sinh} tk_sinh 
         {-> New a_fl_trigonometrica.tk_sinh ( tk_sinh  )  }   |

     {tk_cosh} tk_cosh 
         {-> New a_fl_trigonometrica.tk_cosh ( tk_cosh  )  }   |

     {tk_tanh} tk_tanh 
         {-> New a_fl_trigonometrica.tk_tanh ( tk_tanh  )  }   |

     {tk_coth} tk_coth 
         {-> New a_fl_trigonometrica.tk_coth ( tk_coth  )  }   |

     {tk_sec} tk_sec 
         {-> New a_fl_trigonometrica.tk_sec ( tk_sec  )  }   |

     {tk_csc} tk_csc 
         {-> New a_fl_trigonometrica.tk_csc ( tk_csc  )  }  
     ;

 equacao_trig
         {-> a_exp } =

     {equacao_trig} fl_trigonometrica equacao_trig_pot? fl_chaves 
         {-> New a_exp.a_equacao_trig ( fl_trigonometrica.a_fl_trigonometrica , equacao_trig_pot.a_equacao_trig_pot , fl_chaves.a_exp  )  }  
     ;

 equacao_trig_pot
         {-> a_equacao_trig_pot  } =

     tk_pot fl_chaves 
         {-> New a_equacao_trig_pot ( fl_chaves.a_exp )  }  
     ;

 fl_alfabetogrego
         {-> a_exp  } =

     {tk_alpha} tk_alpha 
         {-> New a_exp.tk_alpha (tk_alpha  )  }   |

     {tk_beta} tk_beta 
         {-> New a_exp.tk_beta (tk_beta  )  }   |

     {tk_gamma} tk_gamma 
         {-> New a_exp.tk_gamma (tk_gamma  )  }   |

     {tk_up_gamma} tk_up_gamma 
         {-> New a_exp.tk_up_gamma (tk_up_gamma  )  }   |

     {tk_delta} tk_delta 
         {-> New a_exp.tk_delta (tk_delta  )  }   |

     {tk_up_delta} tk_up_delta 
         {-> New a_exp.tk_up_delta (tk_up_delta  )  }   |

     {tk_epsilon} tk_epsilon 
         {-> New a_exp.tk_epsilon (tk_epsilon  )  }   |

     {tk_varepsilon} tk_varepsilon 
         {-> New a_exp.tk_varepsilon (tk_varepsilon  )  }   |

     {tk_zeta} tk_zeta 
         {-> New a_exp.tk_zeta (tk_zeta  )  }   |

     {tk_eta} tk_eta 
         {-> New a_exp.tk_eta (tk_eta  )  }   |

     {tk_theta} tk_theta 
         {-> New a_exp.tk_theta (tk_theta  )  }   |

     {tk_up_theta} tk_up_theta 
         {-> New a_exp.tk_up_theta (tk_up_theta  )  }   |

     {tk_vartheta} tk_vartheta 
         {-> New a_exp.tk_vartheta (tk_vartheta  )  }   |

     {tk_iota} tk_iota 
         {-> New a_exp.tk_iota (tk_iota  )  }   |

     {tk_kappa} tk_kappa 
         {-> New a_exp.tk_kappa (tk_kappa  )  }   |

     {tk_varkappa} tk_varkappa 
         {-> New a_exp.tk_varkappa (tk_varkappa  )  }   |

     {tk_lambda} tk_lambda 
         {-> New a_exp.tk_lambda (tk_lambda  )  }   |

     {tk_up_lambda} tk_up_lambda 
         {-> New a_exp.tk_up_lambda (tk_up_lambda  )  }   |

     {tk_mu} tk_mu 
         {-> New a_exp.tk_mu (tk_mu  )  }   |

     {tk_nu} tk_nu 
         {-> New a_exp.tk_nu (tk_nu  )  }   |

     {tk_xi} tk_xi 
         {-> New a_exp.tk_xi (tk_xi  )  }   |

     {tk_up_xi} tk_up_xi 
         {-> New a_exp.tk_up_xi (tk_up_xi  )  }   |

     {tk_pi} tk_pi 
         {-> New a_exp.tk_pi (tk_pi  )  }   |

     {tk_up_pi} tk_up_pi 
         {-> New a_exp.tk_up_pi (tk_up_pi  )  }   |

     {tk_varpi} tk_varpi 
         {-> New a_exp.tk_varpi (tk_varpi  )  }   |

     {tk_rho} tk_rho 
         {-> New a_exp.tk_rho (tk_rho  )  }   |

     {tk_varrho} tk_varrho 
         {-> New a_exp.tk_varrho (tk_varrho  )  }   |

     {tk_sigma} tk_sigma 
         {-> New a_exp.tk_sigma (tk_sigma  )  }   |

     {tk_up_sigma} tk_up_sigma 
         {-> New a_exp.tk_up_sigma (tk_up_sigma  )  }   |

     {tk_varsigma} tk_varsigma 
         {-> New a_exp.tk_varsigma (tk_varsigma  )  }   |

     {tk_tau} tk_tau 
         {-> New a_exp.tk_tau (tk_tau  )  }   |

     {tk_upsilon} tk_upsilon 
         {-> New a_exp.tk_upsilon (tk_upsilon  )  }   |

     {tk_up_upsilon} tk_up_upsilon 
         {-> New a_exp.tk_up_upsilon (tk_up_upsilon  )  }   |

     {tk_phi} tk_phi 
         {-> New a_exp.tk_phi (tk_phi  )  }   |

     {tk_up_phi} tk_up_phi 
         {-> New a_exp.tk_up_phi (tk_up_phi  )  }   |

     {tk_varphi} tk_varphi 
         {-> New a_exp.tk_varphi (tk_varphi  )  }   |

     {tk_chi} tk_chi 
         {-> New a_exp.tk_chi (tk_chi  )  }   |

     {tk_psi} tk_psi 
         {-> New a_exp.tk_psi (tk_psi  )  }   |

     {tk_up_psi} tk_up_psi 
         {-> New a_exp.tk_up_psi (tk_up_psi  )  }   |

     {tk_omega} tk_omega 
         {-> New a_exp.tk_omega (tk_omega  )  }   |

     {tk_up_omega} tk_up_omega 
         {-> New a_exp.tk_up_omega (tk_up_omega  )  }  
     ;

 bloco_inicio  =
 
     {bloco_inicio} tk_begin tk_chave_left tk_equation tk_chave_right   
     ;

 bloco_fim  =

     {bloco_fim} tk_end tk_chave_left tk_equation tk_chave_right   
     ;

 bloco_inicio_array  =

     {bloco_inicio_array} tk_begin tk_chave_left tk_array tk_chave_right 
 	 ;

 bloco_fim_array  =

     {bloco_fim_array} tk_end tk_chave_left tk_array tk_chave_right 
     ;

 fl_colchetes
         {-> a_exp } =

     {fl_colchetes} tk_col_left exp tk_col_right 
         {-> exp.a_exp }  
     ;

 fl_parenteses
         {-> a_exp  } =

     {fl_parenteses} tk_par_left exp tk_par_right 
         {-> exp.a_exp }  
     ;

 fl_chaves
         {-> a_exp  } =

     {fl_chaves} tk_chave_left exp tk_chave_right 
         {-> exp.a_exp }  
     ;
     
     
/*************************************************************************/


Abstract Syntax Tree


/*************************************************************************/

    a_bloco_equacoes =
     {a_equacao} a_equacao |
     {a_lista_equacoes} a_equacao* 
     ;
     
    a_equacao = 
	 tk_identificador a_exp 
	 ;
	
    a_exp =
	 {a_soma} [left]:a_exp [right]:a_exp |
 	 {a_dim} [left]:a_exp [right]:a_exp |
 	 {a_div} [left]:a_exp [right]:a_exp |
 	 {a_mult} [left]:a_exp [right]:a_exp |
  	 {a_mod} [left]:a_exp [right]:a_exp |
     {a_pot} [left]:a_exp [right]:a_exp |
     {a_fl_colchetes} a_exp  |
     {a_fl_parenteses} a_exp |
     {a_fl_chaves} a_exp |
     {a_negativo} a_exp |
     {a_tk_num_inteiro} tk_num_inteiro | 
     {a_tk_num_real} tk_num_real |
     {a_id_modificado} [left]:a_exp [right]:a_exp |
     {a_identificador} tk_identificador | 
     {a_equacao_trig} a_fl_trigonometrica a_equacao_trig_pot? a_exp | 
     {a_fl_operador_custom} tk_identificador a_exp* | 
     {a_fl_style} tk_fl_displaystyle tk_identificador |
 	 {a_fa_times} [left]:a_exp [right]:a_exp |
 	 {a_fa_cdot} [left]:a_exp [right]:a_exp |
 	 {a_fa_sum} [left]:a_exp [right]:a_exp |
 	 {a_fa_prod} [left]:a_exp [right]:a_exp |
     {a_vetora} tk_identificador | 
     {a_vetorb} a_exp |
     {a_fm_min} a_exp* | 
     {a_fm_max} a_exp* | 
     {a_fm_frac} [left]: a_exp [right]: a_exp  | 
     {a_fm_exponencial} a_exp  | 
     {a_fm_sqrt} a_exp  | 
     {a_fm_lim} tk_lim tk_underline tk_chave_left tk_identificador tk_to tk_infty tk_chave_right  | 
     {a_fm_integral} [left]: a_exp [right]: a_exp  |
     {a_parametros} a_exp* |
     {tk_alpha} tk_alpha | 
     {tk_beta} tk_beta | 
     {tk_gamma} tk_gamma | 
     {tk_up_gamma} tk_up_gamma | 
     {tk_delta} tk_delta | 
     {tk_up_delta} tk_up_delta | 
     {tk_epsilon} tk_epsilon | 
     {tk_varepsilon} tk_varepsilon | 
     {tk_zeta} tk_zeta | 
     {tk_eta} tk_eta | 
     {tk_theta} tk_theta | 
     {tk_up_theta} tk_up_theta | 
     {tk_vartheta} tk_vartheta | 
     {tk_iota} tk_iota | 
     {tk_kappa} tk_kappa | 
     {tk_varkappa} tk_varkappa | 
     {tk_lambda} tk_lambda | 
     {tk_up_lambda} tk_up_lambda | 
     {tk_mu} tk_mu | 
     {tk_nu} tk_nu | 
     {tk_xi} tk_xi | 
     {tk_up_xi} tk_up_xi | 
     {tk_pi} tk_pi | 
     {tk_up_pi} tk_up_pi | 
     {tk_varpi} tk_varpi | 
     {tk_rho} tk_rho | 
     {tk_varrho} tk_varrho | 
     {tk_sigma} tk_sigma | 
     {tk_up_sigma} tk_up_sigma | 
     {tk_varsigma} tk_varsigma | 
     {tk_tau} tk_tau | 
     {tk_upsilon} tk_upsilon | 
     {tk_up_upsilon} tk_up_upsilon | 
     {tk_phi} tk_phi | 
     {tk_up_phi} tk_up_phi | 
     {tk_varphi} tk_varphi | 
     {tk_chi} tk_chi | 
     {tk_psi} tk_psi | 
     {tk_up_psi} tk_up_psi | 
     {tk_omega} tk_omega | 
     {tk_up_omega} tk_up_omega 
     ;
     
    a_fl_trigonometrica =
     {tk_cos} tk_cos | 
     {tk_sin} tk_sin | 
     {tk_tan} tk_tan | 
     {tk_cot} tk_cot | 
     {tk_arcsin} tk_arcsin | 
     {tk_arccos} tk_arccos | 
     {tk_arctan} tk_arctan | 
     {tk_arccot} tk_arccot | 
     {tk_sinh} tk_sinh | 
     {tk_cosh} tk_cosh | 
     {tk_tanh} tk_tanh | 
     {tk_coth} tk_coth | 
     {tk_sec} tk_sec | 
     {tk_csc} tk_csc 
     ;

    a_equacao_trig_pot =
      a_exp 
     ;