
/*************************************************************************/
  
Package latexToGLSL;

/*************************************************************************/
    

Helpers


/* Alfabeto Grego */
 
 
 alfabeto_grego = 
 '\alpha'     | 
 '\beta'      | 
 '\gamma'     | 
 '\Gamma'     | 
 '\delta'     | 
 '\Delta'     | 
 '\epsilon'   | 
 '\varepsilon'| 
 '\zeta'      | 
 '\eta'       | 
 '\theta'     | 
 '\Theta'     | 
 '\vartheta'  | 
 '\iota'      |  
 '\kappa'     | 
 '\varkappa'  |
 '\lambda'    | 
 '\Lambda'    | 
 '\mu'        | 
 '\nu'        | 
 '\xi'        | 
 '\Xi'        | 
 '\pi'        | 
 '\Pi'        | 
 '\varpi'     | 
 '\rho'       | 
 '\varrho'    | 
 '\sigma'     | 
 '\Sigma'     | 
 '\varsigma'  |
 '\tau'       | 
 '\upsilon'   |
 '\Upsilon'	  |
 '\phi'       | 
 '\Phi'       | 
 '\varphi'    |
 '\chi'       | 
 '\psi'       | 
 '\Psi'       |
 '\omega' 	  | 
 '\Omega'	  ;


/* Expressoes Numéricas */


 exponencial = '\exp'; //constante de euler


/* Caracteres */


 letra = ['a'..'z'] | ['A'..'Z'];
 digito = ['0'..'9'];
 espaco = ' ';
 tudo = [0..255];
 
 
/* Retorno */


 rc = 13; //carriage return
 nl = 10; //new line
 tab = 9;  //tabulacao
 tudo_sem_retorno = [tudo - [rc + nl]];
 tudo_sem_chave = [tudo - '}'];
 tudo_sem_aspas = [tudo - '''];
 fl_espaco = '\\';
 

/*************************************************************************/


Tokens


/* Palavras reservadas */


 tk_equation = 'equation';
 tk_array = 'array';
 tk_begin = '\begin';
 tk_end = '\end';
 tk_fl_lbrace = '\lbrace';
 tk_fl_rbrace = '\rbrace';
 tk_fl_operatorname = '\operatorname';
 tk_fl_displaystyle = '\displaystyle'; 
 tk_fl_mathbf = '\mathbf';
 tk_fl_sifao = '$';
 

/* Operadores */


 tk_igual = '=';
 tk_soma = '+';
 tk_subt = '-';
 tk_mult = '*';
 tk_div = '/';
 tk_mod = '%';
 tk_pot = '^';
 tk_par_left = '(';
 tk_par_right  = ')';
 tk_col_left  = '[';
 tk_col_right = ']';
 tk_chave_left  = '{';
 tk_chave_right = '}';
 tk_underline = '_';
 tk_virgula = ',';
 tk_pipe = '|';
 tk_maior = '>';
 tk_menor = '<';
 tk_maiorigual = '>=';
 tk_menorigual = '<=';
 
 
/* Funções Algebra Linear */ 
 
 
 tk_times = '\times'; /*cross product*/ 
 tk_cdot = '\cdot';
 tk_vec = '\vec';
 tk_sum = '\sum';
 tk_prod = '\prod';
 
 
/* Funções Matemáticas */ 
 
 
 tk_min = '\min';
 tk_max = '\max';
 tk_frac = '\frac';
 tk_log = '\log';
 tk_ln = '\ln';
 tk_lg = '\lg';
 tk_sqrt = '\sqrt';
 tk_lim = '\lim';
 tk_to = '\to'; 
 tk_infty = '\infty';
 tk_integral = '\int';
 tk_degree = '\circ';
 tk_unf_letra = '\mathrm';
 tk_pipe_mid = '\mid';
 tk_leftarrow = '\leftarrow'; 
 tk_la_gets = '\gets';
 tk_rightarrow = '\rightarrow';
 tk_double_pipe = '\|';
 
 
/* Funções Trigonométricas */ 
	
 tk_cos = '\cos';
 tk_sin = '\sin';
 tk_tan = '\tan';
 tk_cot = '\cot';
 tk_arcsin = '\arcsin';
 tk_arccos = '\arccos';
 tk_arctan = '\arctan';
 tk_arccot = '\arccot';
 tk_sinh = '\sinh';
 tk_cosh = '\cosh';
 tk_tanh = '\tanh';
 tk_coth = '\coth';
 tk_sec = '\sec';
 tk_csc = '\csc';
 

/* Caracteres */


 tk_numero = (digito+) | (digito+ '.' digito+) | exponencial;
 tk_identificador = (letra+) | alfabeto_grego; 


/* Ignorados */


 vazio = (espaco | rc | nl | tab)+;
 espaco = fl_espaco+;
/* comentario_linha = ('--' tudo_sem_retorno*); */
/* comentario_bloco = ('{' tudo_sem_chave* '}')+; */
 
 
/*************************************************************************/

Ignored Tokens

 vazio, espaco;

/*************************************************************************/

Productions 
         
 declaracao_inicial
         {-> a_bloco_equacoes  } =

 	 {declaracao_inicial} bloco_inicio bloco_equacoes bloco_fim  
         {-> bloco_equacoes.a_bloco_equacoes  }  
     ;

 bloco_equacoes 
 		 {-> a_bloco_equacoes  } =
         
 	 {equacao} equacao  
         {-> New a_bloco_equacoes.a_equacao ( equacao.a_equacao ) }   |
         
     {lista_equacoes} bloco_inicio_array lista_equacoes bloco_fim_array 
         {-> New a_bloco_equacoes.a_lista_equacoes ( [ lista_equacoes.a_equacao ] ) }  
     ;    
     
 lista_equacoes
 	 	 {-> a_equacao*  } = 
 	 	 
 	 {equacao} equacao  
         {-> [ equacao.a_equacao ] }   |
         
     {lista_equacoes} equacao tk_virgula lista_equacoes 
   		 {-> [ equacao.a_equacao, lista_equacoes.a_equacao  ] }
     ;
     
 equacao
         {-> a_equacao } =
     {equacao} tk_fl_displaystyle? tk_identificador tk_igual exp
         {-> New a_equacao ( tk_identificador, exp.a_exp ) }
     ;

 parametros
         {-> a_exp* } =
         
     {single} exp
         {-> [ exp.a_exp ]  }  |

     {multiple} exp tk_virgula parametros 
         {-> [ exp.a_exp, parametros.a_exp ]  }  
     ;

 exp
         {-> a_exp  } =

     {fator} fator 
         {-> fator.a_exp }   |

     {soma} exp tk_soma fator 
         {-> New a_exp.a_soma ( exp.a_exp , fator.a_exp  )  }   |

     {dim} exp tk_subt fator 
         {-> New a_exp.a_dim ( exp.a_exp , fator.a_exp  )  }  
     ;

 fator
         {-> a_exp  } =

     {termo} fator_expo 
         {-> fator_expo.a_exp  }   |

     {mult} fator tk_mult fator_expo 
         {-> New a_exp.a_mult ( fator.a_exp, fator_expo.a_exp )  }   |

     {div} fator tk_div fator_expo 
         {-> New a_exp.a_div ( fator.a_exp, fator_expo.a_exp )  }    |

     {mod} fator tk_mod fator_expo 
         {-> New a_exp.a_mod ( fator.a_exp, fator_expo.a_exp )  }  
     ;

 fator_expo
         {-> a_exp  } =

     {termo} termo 
         {-> termo.a_exp  }   |

     {pot} termo tk_pot fator_expo 
         {-> New a_exp.a_pot ( termo.a_exp, fator_expo.a_exp )  }  
     ;

 termo
         {-> a_exp  } =

     {identificador} identificador 
         {-> identificador.a_exp  }   |
     
	 {numero} tk_numero 
         {-> New a_exp.a_numero ( tk_numero )  }   |
             
     {fl_reservado} fl_reservado 
         {-> fl_reservado.a_exp }     |
         
     {vetor_tres} vetor_tres 
         {-> vetor_tres.a_exp  }      |

     {fl_parenteses} fl_parenteses 
         {-> fl_parenteses.a_exp  }   |

     {fl_colchetes} fl_colchetes 
         {-> fl_colchetes.a_exp  }    |

     {fl_chaves} fl_chaves 
         {-> fl_chaves.a_exp  }       |

     {negativo} tk_subt termo 
        { -> New a_exp.a_negativo ( termo.a_exp ) }  
     ;

 vetor_tres
         {-> a_exp  } =

     tk_par_left [x]:exp [a]:tk_virgula [y]:exp [b]:tk_virgula [z]:exp tk_par_right 
         {-> New a_exp.a_vetor_tridimensional ( x.a_exp, y.a_exp, z.a_exp )  }   
     ;

 identificador
         {-> a_exp  } =

     {id} tk_identificador 
         {-> New a_exp.a_id ( tk_identificador  )  }   |

     {id_modificado} identificador tk_underline fl_chaves 
         {-> New a_exp.a_id_modificado ( identificador.a_exp , fl_chaves.a_exp  )  }  
     ;

 fl_reservado
         {-> a_exp  } =

     {fl_algebra} fl_algebra 
         {-> fl_algebra.a_exp }      |

     {fl_matematica} fl_matematica 
         {-> fl_matematica.a_exp }   |

     {equacao_trig} equacao_trig 
         {-> equacao_trig.a_exp }    |

     {fl_operador_custom} tk_fl_operatorname tk_chave_left tk_identificador tk_chave_right tk_par_left parametros tk_par_right 
         {-> New a_exp.a_fl_operador_custom (tk_identificador,  [parametros.a_exp]  )  }   |

     {fl_style} tk_fl_displaystyle tk_identificador 
         {-> New a_exp.a_fl_style (tk_fl_displaystyle , tk_identificador  )  }  
     ;

 fl_algebra
         {-> a_exp  } =

     {times} fa_times 
         {-> fa_times.a_exp }   |

     {cdot} fa_cdot 
         {-> fa_cdot.a_exp }    |

     {sum} fa_sum 
         {-> fa_sum.a_exp }     |

     {prod} fa_prod 
         {-> fa_prod.a_exp }    |

     {vec} fa_vec 
         {-> fa_vec.a_exp }   
     ;

 fa_times
         {-> a_exp  } =

     [lado_esquerdo]: fl_chaves tk_times [lado_direito]: fl_chaves 
         {-> New a_exp.a_fa_times ( lado_esquerdo.a_exp , lado_direito.a_exp )  }  
     ;

 fa_cdot
         {-> a_exp  } =

     [lado_esquerdo]: fl_chaves tk_cdot [lado_direito]: fl_chaves 
         {-> New a_exp.a_fa_cdot ( lado_esquerdo.a_exp , lado_direito.a_exp )  }  
     ;

 fa_sum
         {-> a_exp  } =

     tk_sum tk_underline [lado_esquerdo]: fl_chaves tk_pot [lado_direito]: fl_chaves 
         {-> New a_exp.a_fa_sum ( lado_esquerdo.a_exp , lado_direito.a_exp ) }  
     ;

 fa_prod
         {-> a_exp  } =

     tk_prod tk_underline [lado_esquerdo]: fl_chaves tk_pot [lado_direito]: fl_chaves 
         {-> New a_exp.a_fa_prod ( lado_esquerdo.a_exp , lado_direito.a_exp )  }  
     ;

 fa_vec
         {-> a_exp  } =

     {vetor_id_fa} tk_vec tk_identificador 
         {-> New a_exp.a_vetor_id_fa ( tk_identificador  )  }   |

     {vetor_fa} tk_vec fl_chaves 
         {-> New a_exp.a_vetor_fa ( fl_chaves.a_exp )  }  
     ;

 fl_matematica
         {-> a_exp  } =

     {fm_min} fm_min 
         {-> fm_min.a_exp }    |

     {fm_max} fm_max 
         {-> fm_max.a_exp }    |

     {fm_frac} fm_frac 
         {-> fm_frac.a_exp }   |

     {fm_sqrt} fm_sqrt 
         {-> fm_sqrt.a_exp }   |

     {fm_lim} fm_lim 
         {-> fm_lim.a_exp }    |

     {fm_integral} fm_integral 
         {-> fm_integral.a_exp } 
     ;

 fm_min
         {-> a_exp  } =

     tk_min tk_par_left parametros tk_par_right 
         {-> New a_exp.a_fm_min ( [parametros.a_exp] )  }  
     ;

 fm_max
         {-> a_exp  } =

     tk_max tk_par_left parametros tk_par_right 
         {-> New a_exp.a_fm_max ( [parametros.a_exp] )  }  
     ;

 fm_frac
         {-> a_exp  } =

     tk_frac [numerador]: fl_chaves [denominador]: fl_chaves 
         {-> New a_exp.a_fm_frac ( numerador.a_exp , denominador.a_exp )  }  
     ;

 fm_sqrt
         {-> a_exp  } =

     tk_sqrt fl_chaves 
         {-> New a_exp.a_fm_sqrt ( fl_chaves.a_exp )  }  
     ;

 fm_lim
         {-> a_exp  } =

     tk_lim tk_underline tk_chave_left tk_identificador tk_to tk_infty tk_chave_right 
         {-> New a_exp.a_fm_lim ( tk_lim , tk_underline , tk_chave_left , tk_identificador , tk_to , tk_infty , tk_chave_right  )  }  
     ;

 fm_integral
         {-> a_exp  } =

     tk_integral tk_underline [lado_esquerdo]: fl_chaves tk_pot [lado_direito]: fl_chaves 
         {-> New a_exp.a_fm_integral ( lado_esquerdo.a_exp , lado_direito.a_exp  )  }  
     ;

 fl_trigonometrica
         {-> a_fl_trigonometrica  } =

     {a_cos} tk_cos 
         {-> New a_fl_trigonometrica.a_cos ( tk_cos  )  }   |

     {a_sin} tk_sin 
         {-> New a_fl_trigonometrica.a_sin ( tk_sin  )  }   |

     {a_tan} tk_tan 
         {-> New a_fl_trigonometrica.a_tan ( tk_tan  )  }   |

     {a_cot} tk_cot 
         {-> New a_fl_trigonometrica.a_cot ( tk_cot  )  }   |

     {a_arcsin} tk_arcsin 
         {-> New a_fl_trigonometrica.a_arcsin ( tk_arcsin  )  }   |

     {a_arccos} tk_arccos 
         {-> New a_fl_trigonometrica.a_arccos ( tk_arccos  )  }   |

     {a_arctan} tk_arctan 
         {-> New a_fl_trigonometrica.a_arctan ( tk_arctan  )  }   |

     {a_arccot} tk_arccot 
         {-> New a_fl_trigonometrica.a_arccot ( tk_arccot  )  }   |

     {a_sinh} tk_sinh 
         {-> New a_fl_trigonometrica.a_sinh ( tk_sinh  )  }   |

     {a_cosh} tk_cosh 
         {-> New a_fl_trigonometrica.a_cosh ( tk_cosh  )  }   |

     {a_tanh} tk_tanh 
         {-> New a_fl_trigonometrica.a_tanh ( tk_tanh  )  }   |

     {a_coth} tk_coth 
         {-> New a_fl_trigonometrica.a_coth ( tk_coth  )  }   |

     {a_sec} tk_sec 
         {-> New a_fl_trigonometrica.a_sec ( tk_sec  )  }   |

     {a_csc} tk_csc 
         {-> New a_fl_trigonometrica.a_csc ( tk_csc  )  }  
     ;

 equacao_trig
         {-> a_exp } =

     {equacao_trig} fl_trigonometrica equacao_trig_pot? fl_chaves 
         {-> New a_exp.a_equacao_trig ( fl_trigonometrica.a_fl_trigonometrica , equacao_trig_pot.a_equacao_trig_pot , fl_chaves.a_exp  )  }  
     ;

 equacao_trig_pot
         {-> a_equacao_trig_pot  } =

     tk_pot fl_chaves 
         {-> New a_equacao_trig_pot ( fl_chaves.a_exp )  }  
     ;

 bloco_inicio  =
 
     {bloco_inicio} tk_begin tk_chave_left tk_equation tk_chave_right   
     ;

 bloco_fim  =

     {bloco_fim} tk_end tk_chave_left tk_equation tk_chave_right   
     ;

 bloco_inicio_array  =

     {bloco_inicio_array} tk_begin tk_chave_left tk_array tk_chave_right 
 	 ;

 bloco_fim_array  =

     {bloco_fim_array} tk_end tk_chave_left tk_array tk_chave_right 
     ;

 fl_colchetes
         {-> a_exp } =

     {fl_colchetes} tk_col_left exp tk_col_right 
         {-> exp.a_exp }  
     ;

 fl_parenteses
         {-> a_exp  } =

     {fl_parenteses} tk_par_left exp tk_par_right 
         {-> exp.a_exp }  
     ;

 fl_chaves
         {-> a_exp  } =

     {fl_chaves} tk_chave_left exp tk_chave_right 
         {-> exp.a_exp }  
     ;
     
     
/*************************************************************************/


Abstract Syntax Tree


/*************************************************************************/


    a_bloco_equacoes =
     {a_equacao} a_equacao 			|
     {a_lista_equacoes} a_equacao* 
     ;
     
    a_equacao = 
	 tk_identificador a_exp 
	 ;
	 
	a_equacao_trig_pot =
      a_exp 
     ;
     
	a_fl_trigonometrica =
     {a_cos}		tk_cos		| 
     {a_sin} 		tk_sin 		| 
     {a_tan} 		tk_tan 		| 
     {a_cot} 		tk_cot 		| 
     {a_arcsin} 	tk_arcsin 	| 
     {a_arccos} 	tk_arccos 	| 
     {a_arctan} 	tk_arctan 	| 
     {a_arccot} 	tk_arccot 	| 
     {a_sinh}		tk_sinh 	| 
     {a_cosh}		tk_cosh 	| 
     {a_tanh}		tk_tanh 	| 
     {a_coth} 		tk_coth 	| 
     {a_sec} 		tk_sec 		| 
     {a_csc}		tk_csc 
     ;

    a_exp =
	 {a_soma}                 [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
 	 {a_dim}                  [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
 	 {a_mult}                 [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
  	 {a_mod}                  [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
     {a_div}                  [numerador]:a_exp [denominador]:a_exp         |
     {a_pot}                  [base]:a_exp [expoente]:a_exp                 |
     {a_numero}               tk_numero 									|
     {a_parametros}           a_exp*                                        |
     {a_fl_colchetes}         a_exp                                         |
     {a_fl_parenteses}        a_exp                                         |
     {a_fl_chaves}            a_exp                                         |
     {a_negativo}             a_exp                                         | 
     {a_vetor_fa}             a_exp                                         |
     {a_vetor_id_fa}          tk_identificador                              |
     {a_id}                   tk_identificador                              |
     {a_id_modificado}        [id]:a_exp [modificador]:a_exp                |
     {a_vetor_tridimensional} [x]:a_exp [y]:a_exp [z]:a_exp                 |
     {a_equacao_trig}         a_fl_trigonometrica a_equacao_trig_pot? a_exp | 
     {a_fl_operador_custom}   tk_identificador a_exp*                       | 
     {a_fl_style}             tk_fl_displaystyle tk_identificador           |
 	 {a_fa_times}             [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
 	 {a_fa_cdot}              [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
 	 {a_fa_sum}               [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
 	 {a_fa_prod}              [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
     {a_fm_min}               a_exp*                                        | 
     {a_fm_max}               a_exp*                                        | 
     {a_fm_sqrt}              a_exp                                         | 
     {a_fm_frac}              [numerador]:a_exp [denominador]:a_exp         |  
     {a_fm_integral}          [lado_esquerdo]:a_exp [lado_direito]:a_exp    |
     {a_fm_lim} 			  tk_lim tk_underline tk_chave_left tk_identificador tk_to tk_infty tk_chave_right 
     ;
     
    